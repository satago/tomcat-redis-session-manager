plugins {
  id "com.jfrog.bintray" version "1.3.1"
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'signing'

group = 'com.orangefunction'
version = '3.0.1-SATAGO'
description = 'Tomcat Redis Session Manager is a Tomcat extension to store sessions in Redis'

repositories {
  mavenCentral()
}

compileJava {
  sourceCompatibility = 1.8
  targetCompatibility = 1.8
}

dependencies {
  compileOnly 'org.apache.tomcat:tomcat-catalina:8.5.11'
  compile 'redis.clients:jedis:2.8.0'
  compile 'org.apache.commons:commons-pool2:2.2'
  compile 'commons-codec:commons-codec:1.9'

  testCompile 'junit:junit:4.+'
  testCompile 'org.hamcrest:hamcrest-core:1.3'
  testCompile 'org.hamcrest:hamcrest-library:1.3'
  testCompile 'org.mockito:mockito-all:1.9.5'
  testCompile 'org.apache.tomcat:tomcat-coyote:8.5.11'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
  from sourceSets.main.allSource
  classifier = 'sources'
}

jar.dependsOn sourcesJar
jar.dependsOn javadocJar

artifacts {
  archives javadocJar
  archives sourcesJar
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
      artifact sourcesJar
      artifact javadocJar

      pom.withXml {
        def root = asNode()

        def deps = root.children().find {
          it.name().localPart == 'dependencies'
        }

        root.children().remove(deps)

        root.appendNode('description', project.description)
        root.appendNode('url', 'https://github.com/satago/tomcat-redis-session-manager')

        def license = root.appendNode('licenses').appendNode('license')
        license.appendNode("name", "MIT")
        license.appendNode("url", "http://opensource.org/licenses/MIT")
        license.appendNode("distribution", "repo")

        def developers = root.appendNode("developers")

        def developer = developers.appendNode("developer")
        developer.appendNode('id', 'jcoleman')
        developer.appendNode('name', 'James Coleman')
        developer.appendNode('email', 'jtc331@gmail.com')
        developer.appendNode('url', 'https://github.com/jcoleman')

        developer = developers.appendNode("developer")
        developer.appendNode('id', 'dmitrygusev')
        developer.appendNode('name', 'Dmitry Gusev')
        developer.appendNode('email', 'dmitry.gusev@gmail.com')
        developer.appendNode('url', 'https://github.com/dmitrygusev')

        def pom = new XmlParser().parse(project.file("build/poms/pom-default.xml"))

        pom.dependencies.each {
          root.append(it)
        }
      }
    }
  }
}

bintray {
  user = "$bintray_user"
  key = "$bintray_api_key"
  publications = ['mavenJava']
  pkg {
    repo = 'maven'
    name = 'tomcat-redis-session-manager'
    userOrg = 'satago' // an optional organization name when the repo belongs to one of the user's orgs
    licenses = ['MIT']
    vcsUrl = 'https://github.com/satago/tomcat-redis-session-manager.git'
  }
  dryRun = false // whether to run this as dry-run, without deploying
}
